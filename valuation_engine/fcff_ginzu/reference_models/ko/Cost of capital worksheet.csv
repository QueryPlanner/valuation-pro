"You can use this spreadsheet to compute your cost of capital. You can either use the built-in data to compute key inputs (beta, equity risk premium, default spread) or enter them directly. If you choose to use the built in ERP data, you can update the ERP numbers to reflect a more current mature market premuum (since the spreadsheet uses the start of the year numbers).",,,,,,,,,,,,
,,,,,,,,,,,,
Estimation of Current Cost of Capital,,,,,,Operating Countries ERP calculator,,,,,,
"There are four ways in whch you can estimate your cost of capital. In the first, you can directly input a cost of captial. In the second, you can work through your company's cost of capital inputs in detail, entering business, geogaphical mix and debt, and I will help you estimate a cost of capital. In the third, you can look up the cost of capital for the industry or industries your firm belongs to and in the fourth, you can use a crosssectional distribution  of costs of capital for companies at the start of the year to make your estimate.",,,,,,Country,Revenues,ERP,Weight,Weighted ERP,,
,,,,,,United States,193636,"=IF(H5=0,0,VLOOKUP(G5,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H5>0,H5/$H$18,)","=IF(J5=0,0,I5*J5)",,"The last two rows in each of country/region risk premium tables is set aside for your input to provide you with flexibility to enter some numbers directly. For instance, assume that you have a company that breaks its revenues down into three countries and then puts the rest into ""Rest of the World"". You can enter the ""Rest of the World"" in one of these two rows and enter an equity risk premium for the rest of the world. The easiest way to do that is to go into the country equity risk premium worksheet and change the GDP for the three countries that you have data for to zero and compute the global weighted average ERP for the remaining countries. With the regional worksheet, you can use the last two rows to enter the data for an individual country (usually the domestic country) that might be broken out though the rest of the revenues are broken down by region. You can look up the ERP for the country in the country ERP worksheet."
,,,,,,China,500,"=IF(H6=0,0,VLOOKUP(G6,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H6>0,H6/$H$18,)","=IF(J6=0,0,I6*J6)",,
,,,,,,,,"=IF(H7=0,0,VLOOKUP(G7,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H7>0,H7/$H$18,)","=IF(J7=0,0,I7*J7)",,
,,,,,,,,"=IF(H8=0,0,VLOOKUP(G8,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H8>0,H8/$H$18,)","=IF(J8=0,0,I8*J8)",,
,,,,,,,,"=IF(H9=0,0,VLOOKUP(G9,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H9>0,H9/$H$18,)","=IF(J9=0,0,I9*J9)",,
,,,,,,,,"=IF(H10=0,0,VLOOKUP(G10,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H10>0,H10/$H$18,)","=IF(J10=0,0,I10*J10)",,
Which approach will you be using?,Detailed,,,,,,,"=IF(H11=0,0,VLOOKUP(G11,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H11>0,H11/$H$18,)","=IF(J11=0,0,I11*J11)",,
"If direct input, enter cost of capital to use",0.09,,,,,,,"=IF(H12=0,0,VLOOKUP(G12,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H12>0,H12/$H$18,)","=IF(J12=0,0,I12*J12)",,
Cost of capital based upon approach =,"=IF(B11=""I will input"",B12,IF(B11=""Detailed"",E62,IF(B11=""Industry Average"",B67,B72)))",,,,,,,"=IF(H13=0,0,VLOOKUP(G13,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H13>0,H13/$H$18,)","=IF(J13=0,0,I13*J13)",,
,,,,,,,,"=IF(H14=0,0,VLOOKUP(G14,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H14>0,H14/$H$18,)","=IF(J14=0,0,I14*J14)",,
Approach 1: Detailed Cost of Capital,,,,,,,,"=IF(H15=0,0,VLOOKUP(G15,'Country equity risk premiums'!$A$5:$D$196,4))","=IF(H15>0,H15/$H$18,)","=IF(J15=0,0,I15*J15)",,
Inputs,,,,,,Rest of the World,31335,0.0641,"=IF(H16>0,H16/$H$18,)","=IF(J16=0,0,I16*J16)",,
Equity,,,,,,,,,"=IF(H17>0,H17/$H$18,)","=IF(J17=0,0,I17*J17)",,
Number of Shares outstanding =,='Input sheet'!B21,,,,,Total,=SUM(H5:H17),,=SUM(J5:J17),=SUM(K5:K17),,
Current Market Price per share =,='Input sheet'!B22,,,,,Operating Regions ERP calculator,,,,,,
,,,,,,Region,Revenues,ERP,Weight,Weighted ERP,,
Approach for estimating beta,Single Business(US),,,,,='Country equity risk premiums'!A201,,='Country equity risk premiums'!B201,=H21/$H$32,=I21*J21,,
"If direct input, enter levered beta (or regression beta)",1.2,,,,,='Country equity risk premiums'!A202,5455,='Country equity risk premiums'!B202,=H22/$H$32,=I22*J22,,
Unlevered beta =,"=IF(B21=""Single Business(US)"",VLOOKUP('Input sheet'!B8,'Industry Averages(US)'!A2:G95,7),IF(B21=""Multibusiness(US)"",K48,IF(B21=""Single Business(Global)"",VLOOKUP('Input sheet'!B9,'Industry Average Beta (Global)'!A2:G95,7),'Cost of capital worksheet'!K64)))",,,,,='Country equity risk premiums'!A203,,='Country equity risk premiums'!B203,=H23/$H$32,=I23*J23,,
Riskfree Rate =,='Input sheet'!B34,,,,,='Country equity risk premiums'!A204,,='Country equity risk premiums'!B204,=H24/$H$32,=I24*J24,,
What approach do you want to use to input ERP?,Operating regions,,,,,='Country equity risk premiums'!A205,5830,='Country equity risk premiums'!B205,=H25/$H$32,=I25*J25,,
"Direct input for ERP (if you choose ""will input""",0.06,,,,,='Country equity risk premiums'!A206,,='Country equity risk premiums'!B206,=H26/$H$32,=I26*J26,,
Equity Risk Premium used in cost of equity =,"=IF(B25=""Will Input"",B26,IF(B25=""Country of Incorporation"",VLOOKUP('Input sheet'!B7,'Country equity risk premiums'!A5:E195,4),IF(B25=""Operating regions"",'Cost of capital worksheet'!K32,'Cost of capital worksheet'!K18)))",,,,,='Country equity risk premiums'!A207,,='Country equity risk premiums'!B207,=H27/$H$32,=I27*J27,,
,,,,,,='Country equity risk premiums'!A208,16774,='Country equity risk premiums'!B208,=H28/$H$32,=I28*J28,,
Debt,,,,,,='Country equity risk premiums'!A209,,='Country equity risk premiums'!B209,=H29/$H$32,=I29*J29,,
Book Value of Straight Debt =,='Input sheet'!B15,,,,,EMEA,8078,0.0587,=H30/$H$32,=I30*J30,,
Interest Expense on Debt =,='Input sheet'!B13,,,,,Rest of the World,10924,0.055,=H31/$H$32,=I31*J31,,
Average Maturity =,3,,,,,Total,=SUM(H21:H31),,=SUM(J21:J31),=SUM(K21:K31),,
Approach for estimating pre-tax cost of debt,Actual rating,,,,,,,,,,,
"If direct input, input the pre-tax cost of debt",0.04,,,,,Multi Business (US Industry Averages),,,,,,
"If actual rating, input the rating",A1/A+,,,,,Business,Revenues,EV/Sales,Estimated Value,Unlevered Beta,Cost of capital,
"If synethetic rating, input the type of company",2,,,,,Computers/Peripherals,195010,"=IF(G36=0,,VLOOKUP(G36,'Industry Averages(US)'!$A$2:$S$95,15))",=H36*I36,"=IF(I36=0,0,VLOOKUP(G36,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I36=0,0,VLOOKUP(G36,'Industry Averages(US)'!$A$2:$S$95,13))",
Pre-tax Cost of Debt =,"=IF(B33=""Direct Input"",B34,IF(B33=""Synthetic Rating"",'Synthetic rating'!D16,B24+VLOOKUP('Cost of capital worksheet'!B35,'Synthetic rating'!G42:H56,2)))",,,,,Electronics (General),43000,"=IF(G37=0,,VLOOKUP(G37,'Industry Averages(US)'!$A$2:$S$95,15))",=H37*I37,"=IF(I37=0,0,VLOOKUP(G37,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I37=0,0,VLOOKUP(G37,'Industry Averages(US)'!$A$2:$S$95,13))",
Tax Rate =,='Input sheet'!B24,,,,,Entertainment,15000,"=IF(G38=0,,VLOOKUP(G38,'Industry Averages(US)'!$A$2:$S$95,15))",=H38*I38,"=IF(I38=0,0,VLOOKUP(G38,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I38=0,0,VLOOKUP(G38,'Industry Averages(US)'!$A$2:$S$95,13))",
,,,,,,,,"=IF(G39=0,,VLOOKUP(G39,'Industry Averages(US)'!$A$2:$S$95,15))",=H39*I39,"=IF(I39=0,0,VLOOKUP(G39,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I39=0,0,VLOOKUP(G39,'Industry Averages(US)'!$A$2:$S$95,13))",
Book Value of Convertible Debt =,0,,,,,,,"=IF(G40=0,,VLOOKUP(G40,'Industry Averages(US)'!$A$2:$S$95,15))",=H40*I40,"=IF(I40=0,0,VLOOKUP(G40,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I40=0,0,VLOOKUP(G40,'Industry Averages(US)'!$A$2:$S$95,13))",
Interest Expense on Convertible =,0,,,,,,,"=IF(G41=0,,VLOOKUP(G41,'Industry Averages(US)'!$A$2:$S$95,15))",=H41*I41,"=IF(I41=0,0,VLOOKUP(G41,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I41=0,0,VLOOKUP(G41,'Industry Averages(US)'!$A$2:$S$95,13))",
Maturity of Convertible Bond =,0,,,,,,,"=IF(G42=0,,VLOOKUP(G42,'Industry Averages(US)'!$A$2:$S$95,15))",=H42*I42,"=IF(I42=0,0,VLOOKUP(G42,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I42=0,0,VLOOKUP(G42,'Industry Averages(US)'!$A$2:$S$95,13))",
Market Value of Convertible =,0,,,,,,,"=IF(G43=0,,VLOOKUP(G43,'Industry Averages(US)'!$A$2:$S$95,15))",=H43*I43,"=IF(I43=0,0,VLOOKUP(G43,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I43=0,0,VLOOKUP(G43,'Industry Averages(US)'!$A$2:$S$95,13))",
,,,,,,,,"=IF(G44=0,,VLOOKUP(G44,'Industry Averages(US)'!$A$2:$S$95,15))",=H44*I44,"=IF(I44=0,0,VLOOKUP(G44,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I44=0,0,VLOOKUP(G44,'Industry Averages(US)'!$A$2:$S$95,13))","The costt of capital  is in US dollars, using the riskfree rate at the start of the year. It will be adjusted for the difference in riskfree rates to make it more current and to change currencies."
Debt value of operating leases =,"=IF('Input sheet'!B17=""Yes"",'Operating lease converter'!F33,0)",,,,,,,"=IF(G45=0,,VLOOKUP(G45,'Industry Averages(US)'!$A$2:$S$95,15))",=H45*I45,"=IF(I45=0,0,VLOOKUP(G45,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I45=0,0,VLOOKUP(G45,'Industry Averages(US)'!$A$2:$S$95,13))",
,,,,,,,,"=IF(G46=0,,VLOOKUP(G46,'Industry Averages(US)'!$A$2:$S$95,15))",=H46*I46,"=IF(I46=0,0,VLOOKUP(G46,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I46=0,0,VLOOKUP(G46,'Industry Averages(US)'!$A$2:$S$95,13))",
Preferred Stock,,,,,,,,"=IF(G47=0,,VLOOKUP(G47,'Industry Averages(US)'!$A$2:$S$95,15))",=H47*I47,"=IF(I47=0,0,VLOOKUP(G47,'Industry Averages(US)'!$A$2:$S$95,7))","=IF(I47=0,0,VLOOKUP(G47,'Industry Averages(US)'!$A$2:$S$95,13))",
Number of Preferred Shares =,0,,,,,Company,=SUM(H36:H47),,=SUM(J36:J47),=K36*(J36/J48)+K37*J37/J48+K38*J38/J48+K39*J39/J48+K40*J40/J48+K41*J41/J48+K42*J42/J48+K43*J43/J48+K44*J44/J48+K45*J45/J48+K46*J46/J48+K47*J47/J48,=L36*(J36/J48)+L37*J37/J48+L38*J38/J48+L39*J39/J48+L40*J40/J48+L41*J41/J48+L42*J42/J48+L43*J43/J48+L44*J44/J48+L45*J45/J48+L46*J46/J48+L47*J47/J48,
Current Market Price per Share=,70,,,,,,,,,,,
Annual Dividend per Share =,5,,,,,Multi Business (Global Industry Averages),,,,,,
,,,,,,Business,Revenues,EV/Sales,Estimated Value,Unlevered Beta,Cost of capital,
Output,,,,,,Electronics (General),81363,"=IF(G52=0,,VLOOKUP(G52,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H52*I52,"=IF(G52=0,,VLOOKUP(G52,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I52=0,0,VLOOKUP(G52,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Estimating Market Value of Straight Debt =,,=B31*(1-(1+B37)^(-B32))/B37+B30/(1+B37)^B32,,,,,,"=IF(G53=0,,VLOOKUP(G53,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H53*I53,"=IF(G53=0,,VLOOKUP(G53,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I53=0,0,VLOOKUP(G53,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Estimated Value of Straight Debt in Convertible =,,=B41*(1-(1+B37)^(-B42))/B37+B40/(1+B37)^B42,,,,,,"=IF(G54=0,,VLOOKUP(G54,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H54*I54,"=IF(G54=0,,VLOOKUP(G54,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I54=0,0,VLOOKUP(G54,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Value of Debt in Operating leases =,,=B45,,,,,,"=IF(G55=0,,VLOOKUP(G55,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H55*I55,"=IF(G55=0,,VLOOKUP(G55,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I55=0,0,VLOOKUP(G55,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Estimated Value of Equity in Convertible =,,=B43-C54,,,,,,"=IF(G56=0,,VLOOKUP(G56,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H56*I56,"=IF(G56=0,,VLOOKUP(G56,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I56=0,0,VLOOKUP(G56,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Levered Beta for equity =,,"=IF(B21=""Direct Input"",B22,B23*(1+(1-B38)*(C60/B60)))",,,,,,"=IF(G57=0,,VLOOKUP(G57,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H57*I57,"=IF(G57=0,,VLOOKUP(G57,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I57=0,0,VLOOKUP(G57,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
,,,,,,,,"=IF(G58=0,,VLOOKUP(G58,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H58*I58,"=IF(G58=0,,VLOOKUP(G58,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I58=0,0,VLOOKUP(G58,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
,Equity,Debt ,Preferred Stock,Capital,,,,"=IF(G59=0,,VLOOKUP(G59,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H59*I59,"=IF(G59=0,,VLOOKUP(G59,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I59=0,0,VLOOKUP(G59,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Market Value,=B18*B19,=C53+C54+C55,=B48*B49,=SUM(B60:D60),,,,"=IF(G60=0,,VLOOKUP(G60,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H60*I60,"=IF(G60=0,,VLOOKUP(G60,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I60=0,0,VLOOKUP(G60,'Industry Average Beta (Global)'!$A$2:$M$95,13))","The cost of capital  is in US dollars, using the riskfree rate at the start of the year. It will be adjusted for the difference in riskfree rates to make it more current and to change currencies."
Weight in Cost of Capital,=B60/$E$60,=C60/$E$60,=D60/$E$60,=SUM(B61:D61),,,,"=IF(G61=0,,VLOOKUP(G61,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H61*I61,"=IF(G61=0,,VLOOKUP(G61,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I61=0,0,VLOOKUP(G61,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
Cost of Component,=B24+C57*B27,=B37*(1-B38),=B50/B49,=B61*B62+C61*C62+D61*D62,,,,"=IF(G62=0,,VLOOKUP(G62,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H62*I62,"=IF(G62=0,,VLOOKUP(G62,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I62=0,0,VLOOKUP(G62,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
,,,,,,,,"=IF(G63=0,,VLOOKUP(G63,'Industry Average Beta (Global)'!$A$2:$O$95,15))",=H63*I63,"=IF(G63=0,,VLOOKUP(G63,'Industry Average Beta (Global)'!$A$2:$O$95,7))","=IF(I63=0,0,VLOOKUP(G63,'Industry Average Beta (Global)'!$A$2:$M$95,13))",
,,,,,,Company,=SUM(H52:H63),,=SUM(J52:J63),=K52*(J52/J64)+K53*J53/J64+K54*J54/J64+K55*J55/J64+K56*J56/J64+K57*J57/J64+K58*J58/J64+K59*J59/J64+K60*J60/J64+K61*J61/J64+K62*J62/J64+K63*J63/J64,=L52*(J52/J64)+L53*J53/J64+L54*J54/J64+L55*J55/J64+L56*J56/J64+L57*J57/J64+L58*J58/J64+L59*J59/J64+L60*J60/J64+L61*J61/J64+L62*J62/J64+L63*J63/J64,
"Approach 2: industry average cost of capital, adjusted for riskfre rate differences",,,,,,,,,,,,
Industry ,Multibusiness(US),,,,,,,,,,,
"Cost of capital, adjusted for riskfree rate difference","=IF(B66=""Single Business(US)"",(VLOOKUP('Input sheet'!B8,'Industry Averages(US)'!A2:M95,13)+('Input sheet'!B34-4.58%)),IF(B66=""Multibusiness(US)"",L48+('Input sheet'!B34-4.58%),IF(B66=""Single Business(Global)"",(VLOOKUP('Input sheet'!B8,'Industry Average Beta (Global)'!A2:M95,13)+('Input sheet'!B34-4.58%)),L64+('Input sheet'!B34-4.58%))))",,,,,,,,,,,
,,,,,,,,,,,,
Approach 3: Uae histogram of costs of capital of all publicly traded firms,,,,,,,,,,,,
"Which grouping (US, Emerging Markets, Global)",US,,,,,,,,,,,
Which risk grouping does your company fall in?,Median,,,,,,,,,,,
Cost of capital based upon decile/group chosen,"=VLOOKUP(B70,A75:F79,IF(B71=""First Decile"",2,IF(B71=""First Quartile"",3,IF(B71=""Median"",4,IF(B71=""Third Quartile"",5,6)))))",,,,,,,,,,,
,,,,,,,,,,,,
Region,First Decile,First Quartile,Median,Third Quartile,Ninth Decile,,,,,,,
Emerging,='Input sheet'!$B$34+2.83%,='Input sheet'!$B$34+3.86%,='Input sheet'!$B$34+5.03%,='Input sheet'!$B$34+6.41%,='Input sheet'!$B$34+8.23%,,,,,,,
Europe,='Input sheet'!$B$34+2.2%,='Input sheet'!$B$34+3.07%,='Input sheet'!$B$34+4.39%,='Input sheet'!$B$34+5.53%,='Input sheet'!$B$34+6.94%,,,,,,,
Global,='Input sheet'!$B$34+2.31%,='Input sheet'!$B$34+3.45%,='Input sheet'!$B$34+4.48%,='Input sheet'!$B$34+5.73%,='Input sheet'!$B$34+7.17%,,,,,,,
Japan,='Input sheet'!$B$34+2.28%,='Input sheet'!$B$34+3.25%,='Input sheet'!$B$34+4.33%,='Input sheet'!$B$34+5.19%,='Input sheet'!$B$34+6.33%,,,,,,,
US,='Input sheet'!$B$34+1.2%,='Input sheet'!$B$34+2.29%,='Input sheet'!$B$34+3.77%,='Input sheet'!$B$34+4.73%,='Input sheet'!$B$34+5.47%,,,,,,,
"These costs of capital are all in US dollars and reflect the US dollar  riskfree rate at the start of the year. Once you make your choice, though, I will adjust the rate by the difference in riskfree rates, effectively converting currency and bringing it up to date. ",,,,,,,,,,,,